10 REM ########################################
20 REM ### SNAKE GAME WITHOUT USING "DIM"   ###
30 REM ########################################
40 REM WIDTH = 79
50 REM HEIGHT = 58
60 FG = 0:BG = 0:GOSUB 770: REM SETCOLOR
70 PRINT CHR$(147);
80 FG = 8:BG = 2:GOSUB 770: REM SETCOLOR
90 FOR X=0 TO 79
100 Y = 0:GOSUB 730: REM SETPOS
110 PRINT "#";
120 Y = 58:GOSUB 730: REM SETPOS
130 PRINT "#";
140 NEXT
150 FOR Y=1 TO 58
160 X = 0:GOSUB 730: REM SETPOS
170 PRINT "#";
180 X = 79:GOSUB 730: REM SETPOS
190 PRINT "#";
200 NEXT
210 FG = 3:BG = 0:GOSUB 770: REM SETCOLOR
220 REM GAME INIT
230 SN$ =  CHR$(60) + CHR$(20) + CHR$(60) + CHR$(20)
240 SN$ = SN$ + CHR$(60) + CHR$(20) + CHR$(60) + CHR$(20)
250 SPD = 10
260 SD$ = "A":CLC=TI+SPD
270 GOSUB 810: REM PUTNEWFOOD
280 REM --------------------------------
290 REM GAME LOOPP
300 X = ASC(SN$)
310 Y = ASC(MID$(SN$,2,1))
320 GOSUB 730: REM SETPOS
330 PRINT " ";
340 GET KEY$
350 IF KEY$ = "W" THEN SD$ = "W"
360 IF KEY$ = "A" THEN SD$ = "A"
370 IF KEY$ = "S" THEN SD$ = "S"
380 IF KEY$ = "D" THEN SD$ = "D"
390 X = ASC(RIGHT$(SN$,2))
400 Y = ASC(RIGHT$(SN$,1))
410 IF SD$ = "W" THEN Y=Y-1
420 IF SD$ = "A" THEN X=X-1
430 IF SD$ = "S" THEN Y=Y+1
440 IF SD$ = "D" THEN X=X+1
450 CH$ = CHR$(VPEEK(0, Y*256+X*2))
460 IF CH$ = " " THEN GOTO 490
470 IF CH$ = CHR$($24) THEN GOTO 510
480 GOTO 610
490 SN$ = MID$(SN$,3) + CHR$(X) + CHR$(Y)
500 GOTO 540
510 SN$ = SN$ + CHR$(X) + CHR$(Y)
520 GOSUB 810: REM PUTNEWFOOD
530 CW = (LEN(SN$)/2):GOSUB 920: REM PUTNEWWALL
540 REM CAN GO
550 GOSUB 730: REM SETPOS
560 PRINT "*";
570 IF CLC>TI THEN GOTO 570
580 CLC = TI+SPD
590 GOTO 290
600 END
610 REM -------- GAME OVER ---------------
620 GOSUB 730: REM SETPOS
630 FG = 2:BG = 0:GOSUB 770: REM SETCOLOR
640 PRINT CHR$($76);
650 X = 35:Y = 23:GOSUB 730: REM SETPOS
660 PRINT "GAME OVER";
670 X = 37:Y = 25:GOSUB 730: REM SETPOS
680 PRINT (LEN(SN$)/2);
690 FG = 1:BG = 0:GOSUB 770: REM SETCOLOR
700 X = 0:Y = 57:GOSUB 730: REM SETPOS
710 END
720 REM --------------------------------
730 REM SETPOS(X,Y)
740 POKE $030D,Y: POKE $030E,X: POKE $030F,0: SYS $FFF0
750 RETURN
760 REM --------------------------------
770 REM SETCOLOR(FG,BG)
780 POKE 646, (BG AND $F) * 16 + (FG AND $F)
790 RETURN
800 REM --------------------------------
810 REM PUTNEWFOOD()
820 XX=X:YY=Y
830 X = INT(RND(TI)*79)+1: Y = INT(RND(TI)*58)+1
840 IF VPEEK(0,Y*256+X*2) <> 32 THEN GOTO 830
850 GOSUB 730: REM SETPOS
860 FG = 0:BG = 13:GOSUB 770: REM SETCOLOR
870 PRINT CHR$($24);
880 FG = 3:BG = 0:GOSUB 770: REM SETCOLOR
890 X=XX:Y=YY
900 RETURN
910 REM --------------------------------
920 REM PUTNEWWALL(CW)
930 XX=X:YY=Y
940 X = INT(RND(TI)*79)+1: Y = INT(RND(TI)*58)+1
950 IF VPEEK(0,Y*256+X*2) <> 32 THEN GOTO 940
960 GOSUB 730: REM SETPOS
970 FG = 8:BG = 2:GOSUB 770: REM SETCOLOR
980 PRINT "#";
990 CW=CW-1
1000 IF CW>0 THEN GOTO 940
1010 FG = 3:BG = 0:GOSUB 770: REM SETCOLOR
1020 X=XX:Y=YY
1030 RETURN
