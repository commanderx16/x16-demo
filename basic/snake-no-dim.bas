10 REM ########################################
20 REM ### SNAKE GAME WITHOUT USING "DIM"   ###
30 REM ########################################
40 FG = 0:BG = 0:GOSUB 750: REM SETCOLOR
50 PRINT CHR$(147);
60 FG = 8:BG = 2:GOSUB 750: REM SETCOLOR
70 FOR X=0 TO 79
80 Y = 0:GOSUB 710: REM SETPOS
90 PRINT "#";
100 Y = 58:GOSUB 710: REM SETPOS
110 PRINT "#";
120 NEXT
130 FOR Y=1 TO 57
140 X = 0:GOSUB 710: REM SETPOS
150 PRINT "#";
160 X = 79:GOSUB 710: REM SETPOS
170 PRINT "#";
180 NEXT
190 FG = 3:BG = 0:GOSUB 750: REM SETCOLOR
200 REM GAME INIT
210 SN$ =  CHR$(60) + CHR$(20) + CHR$(60) + CHR$(20)
220 SN$ = SN$ + CHR$(60) + CHR$(20) + CHR$(60) + CHR$(20)
230 SPD = 10
240 SD$ = "A":CLC=TI+SPD
250 GOSUB 790: REM PUTNEWFOOD
260 REM --------------------------------
270 REM GAME LOOPP
280 X = ASC(SN$)
290 Y = ASC(MID$(SN$,2,1))
300 GOSUB 710: REM SETPOS
310 PRINT " ";
320 GET KEY$
330 IF KEY$ = "W" THEN SD$ = "W"
340 IF KEY$ = "A" THEN SD$ = "A"
350 IF KEY$ = "S" THEN SD$ = "S"
360 IF KEY$ = "D" THEN SD$ = "D"
370 X = ASC(RIGHT$(SN$,2))
380 Y = ASC(RIGHT$(SN$,1))
390 IF SD$ = "W" THEN Y=Y-1
400 IF SD$ = "A" THEN X=X-1
410 IF SD$ = "S" THEN Y=Y+1
420 IF SD$ = "D" THEN X=X+1
430 CH$ = CHR$(VPEEK(0, Y*256+X*2))
440 IF CH$ = " " THEN GOTO 470
450 IF CH$ = CHR$($24) THEN GOTO 490
460 GOTO 590
470 SN$ = MID$(SN$,3) + CHR$(X) + CHR$(Y)
480 GOTO 520
490 SN$ = SN$ + CHR$(X) + CHR$(Y)
500 GOSUB 790: REM PUTNEWFOOD
510 CW = (LEN(SN$)/2):GOSUB 900: REM PUTNEWWALL
520 REM CAN GO
530 GOSUB 710: REM SETPOS
540 PRINT "*";
550 IF CLC>TI THEN GOTO 550
560 CLC = TI+SPD
570 GOTO 270
580 END
590 REM -------- GAME OVER ---------------
600 GOSUB 710: REM SETPOS
610 FG = 2:BG = 0:GOSUB 750: REM SETCOLOR
620 PRINT CHR$($76);
630 X = 35:Y = 23:GOSUB 710: REM SETPOS
640 PRINT "GAME OVER";
650 X = 37:Y = 25:GOSUB 710: REM SETPOS
660 PRINT (LEN(SN$)/2);
670 FG = 1:BG = 0:GOSUB 750: REM SETCOLOR
680 X = 0:Y = 57:GOSUB 710: REM SETPOS
690 END
700 REM --------------------------------
710 REM SETPOS(X,Y)
720 POKE $030D,Y: POKE $030E,X: POKE $030F,0: SYS $FFF0
730 RETURN
740 REM --------------------------------
750 REM SETCOLOR(FG,BG)
760 POKE 646, (BG AND $F) * 16 + (FG AND $F)
770 RETURN
780 REM --------------------------------
790 REM PUTNEWFOOD()
800 XX=X:YY=Y
810 X = INT(RND(TI)*78)+1: Y = INT(RND(TI)*48)+1
820 IF VPEEK(0,Y*256+X*2) <> 32 THEN GOTO 810
830 GOSUB 710: REM SETPOS
840 FG = 0:BG = 13:GOSUB 750: REM SETCOLOR
850 PRINT CHR$($24);
860 FG = 3:BG = 0:GOSUB 750: REM SETCOLOR
870 X=XX:Y=YY
880 RETURN
890 REM --------------------------------
900 REM PUTNEWWALL(CW)
910 XX=X:YY=Y
920 X = INT(RND(TI)*78)+1: Y = INT(RND(TI)*48)+1
930 IF VPEEK(0,Y*256+X*2) <> 32 THEN GOTO 920
940 GOSUB 710: REM SETPOS
950 FG = 8:BG = 2:GOSUB 750: REM SETCOLOR
960 PRINT "#";
970 CW=CW-1
980 IF CW>0 THEN GOTO 920
990 FG = 3:BG = 0:GOSUB 750: REM SETCOLOR
1000 X=XX:Y=YY
1010 RETURN
