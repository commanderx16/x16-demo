5 REM BY STEVE CLARK https://www.facebook.com/groups/CommanderX16/permalink/518653948885704/
10 SCREEN 2 : REM SWITCH TO 80 CHARACTER MODE
20 FOR I=1 TO 60:PRINT:NEXT
30 RI=(60-42)/2:CI=(80-66)/2
110 X=$40:FOR I= 0 TO  5:R0=RI+I:C0=CI+   I:GOSUB 220:NEXT
120 X=$E0:FOR I= 6 TO 11:R0=RI+I:C0=CI+   I:GOSUB 220:NEXT
130 X=$30:FOR I=12 TO 17:R0=RI+I:C0=CI+   I:GOSUB 220:NEXT
140 X=$50:FOR I=18 TO 23:R0=RI+I:C0=CI+24  :GOSUB 220:NEXT
150 X=$70:FOR I=24 TO 29:R0=RI+I:C0=CI+42-I:GOSUB 220:NEXT
160 X=$A0:FOR I=30 TO 35:R0=RI+I:C0=CI+42-I:GOSUB 220:NEXT
170 X=$20:FOR I=36 TO 41:R0=RI+I:C0=CI+42-I:GOSUB 220:NEXT
180 GOTO 320
210 REM SUBROUTINE
220 C1=17:IF (C0+C1)>(40-4) THEN C1=40-C0-4
230 FOR C=0 TO C1
240 VPOKE 0,256*(R0+R)+2*(   C0+C)+1,X
250 VPOKE 0,256*(R0+R)+2*(80-C0-C)-1,X
260 NEXT
270 RETURN
310 REM GET A RANDOM LOCATION IN LEFT HALF
320 RL=INT(RND(1)*60):CL=INT(RND(1)*80):IF CL>40 THEN GOTO 320
330 XL=VPEEK(0,256*RL+2*CL+1):REM SAVE THE CURRENT BLOCK COLOR
340 IF (XL AND $F0)=$60 THEN GOTO 320
350 REM GET A RANDOM LOCATION IN RIGHT HALF
360 RR=INT(RND(1)*60):CR=INT(RND(1)*80):IF CR<41 THEN GOTO 360
370 XR=VPEEK(0,256*RR+2*CR+1):REM SAVE THE CURRENT BLOCK COLOR
380 IF (XR AND $F0)=$60 THEN GOTO 350
410 REM LOOP
420 X=XL:R=RL:C=CL:GOSUB 520
430 XL=X:RL=R:CL=C
440 X=XR:R=RR:C=CR:GOSUB 520
450 XR=X:RR=R:CR=C
460 GOTO 410:REM LOOP
510 REM SUBROUTINE
520 D=INT(RND(1)*4):REM GET A RANDOM DIRECTION
530 R1=R:C1=C
540 IF D=0 THEN R1=R-1:GOTO 600:REM MOVE UP
550 IF D=1 THEN R1=R+1:GOTO 600:REM MOVE DOWN
560 IF D=2 THEN C1=C-1:GOTO 600:REM MOVE LEFT
570 IF D=3 THEN C1=C+1:GOTO 600:REM MOVE RIGHT
580 STOP:REM SHOULD NEVER GET HERE
590 REM MOVE
600 Y=VPEEK(0,256*R1+2*C1+1):REM GET THE NEW BLOCK COLOR
610 IF (Y AND $F0)=$60 THEN GOTO 640
620 VPOKE 0,256*R+2*C+1,Y:VPOKE 0,256*R1+2*C1+1,X
630 X=Y:R=R1:C=C1
640 RETURN
