5 REM LEMONADE STAND FOR X16, EDIT 2021 ROBERT EAGLESTONE
10 REM BORING NOTES AT THE END OF THIS LISTING
15 REM
20 REM TO USE THE PET FONT:
25 REM
30 VLOAD "PETFONT.BIN",8,1,0
35 POKE $9F36,128
40 DIM A0$(30)
45 A0$(0)="\X13"
50 FORX=1TO30:A0$(X)=A0$(X-1)+"\X11":NEXT
55 REM ------------------------------------
60 REM
65 REM
70 REM  COMMODORE TITLE PAGE (CA. 1986)
75 REM
80 REM
85 REM ------------------------------------
90 SCREEN 0  :REM X16 40X25 MODE
95 COLOR 5,0 :REM X16 GREEN-ON-BLACK
100 ?CHR$(147)
105 PRINT"\X93\X11\X1D";
110 FORI=1TO38:PRINT"\X12Š\X92";:NEXT
115 FORI=1TO27:PRINT"\X11\X9D\X12Š\X92";:NEXT
120 FORI=1TO37:PRINT"\X9D\X9D\X12Š\X92";:NEXT
125 FORI=1TO27:PRINT"\X91\X9D\X12Š\X92";:NEXT
130 REM ------------------------------------
135 REM
140 REM  PRINT THE COMMODORE LOGO!
145 REM
150 REM ------------------------------------
155 COLOR 6,0 :REM LTBLUE ON BLACK
160 PRINT"\X13\X11\X11\X11\X11\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12©  \X92
165 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  \X12 \X92©
170 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  ";
175 COLOR 2,0 :REM RED ON BLACK
180 PRINT "\X12£ß
185 COLOR 6,0 :REM LTBLUE ON BLACK
190 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1Dß\X12  \X92
195 COLOR 5,0 :REM X16 GREEN-ON-BLACK
200 PRINT"\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DC O M M O D O R E\X11\X11
205 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DEDUCATIONAL\X11\X11
210 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DSOFTWARE
215 ? A0$(24);"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12    PRESS SPACE BAR    \X92":I=0
220 GET A$: IF A$<>" " GOTO 220
225 GOSUB 2120 :REM INTRO
230 V=RND(-TI) :REM SEED RANDOMIZER
235 ?"\X93\X11\X11 LEMONADE STAND \X11\X11"
240 INPUT" WHAT IS YOUR NAME";N$
245 IF N$="" GOTO 90 
250 N$=LEFT$(N$,13)
255 ?
260 REM ------------------------------------
265 REM
270 REM
275 REM  INITIALIZATION
280 REM
285 REM
290 REM ------------------------------------
295 WK=12 :REM # OF WEEKS TO RUN
300 B=10  :REM STARTING CASH
305 D1=0  :REM STARTING LEMON CONCENTRATE
310 A1=0  :REM STARTING SUGAR
315 P1=0  :REM STARTING CUPS
320 X=1
325 ?:?" " N$", YOU SHALL OPERATE YOUR"
330 ?:?" STAND FOR"WK"WEEKS."
335 GOSUB 1745 :REM SPACEBAR
340 REM ------------------------------------
345 REM
350 REM
355 REM  MAIN LOOP
360 REM
365 REM
370 REM ------------------------------------
375 FX=0
380 ? "\X93\X11";
385 ?" WEEK NUMBER" X
390 ? "
395 ? "                       YOU HAVE:
400 ? "
405 ? "    §£££££££££££££££¥         QTS/CONC
410 ? "    §               ¥         LBS/SUGAR
415 TA=4+INT(((14-LEN(N$))/2)+.5)
420 ? TAB(TA)"\X91"N$"'S"
425 ? "    §   LEMONADE    ¥         CUPS
430 ? "    §               ¥         
435 ? "    §        CENTS  ¥  WEATHER:
440 ? "    §£££££££££££££££¥  
445 ? "    § €€€           ¥  
450 ? "    § Í ÎÉ          ¥
455 ? "    § ( )Ë¡¡¡¡¡¡¡¡¡ ¥
460 ? "    §\X12©             ß\X92¥
465 ? "    \X12©               ß\X92
470 ? "    È(Ù@Ç()ÕÉÇ()Ç)Ç*Ç
475 ? "    È)Ù(Â)(ÊËÂ)(Â@Ç)Ç
480 ? "    È(Ù)Ç(×()Ç(*Ç)Ç(Ç
485 ? "    È)Ù(Â)(@(Â)(Â(Ç)Ç
490 ? "    È*Ù)Ç*)()Ç()Ç)Ç(Ç
495 ? "    È)Ù(Ç)()(Ç)(Ç(Ç)Ç
500 ? "±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
505 GOSUB 1640 :REM SHOW VALUES
510 REM ------------------------------------
515 REM
520 REM  PREDICTIONS
525 REM
530 REM  T: TEMP
535 REM  H: HUMIDITY
540 REM  S: COST OF SUGAR
545 REM  L: COST OF LEMON CONCENTRATE
550 REM  C: COST OF CUPS
555 REM  Z: TEMP VARIATION (ACTUAL)
560 REM  T2: FELT (LOW) TEMPERATURE W/HUMIDITY
565 REM  F2,F4: THRESHOLD VALUES
570 REM
575 REM ------------------------------------
580 T=(INT(RND(1)*3)+7)*10
585 H=RND(1)
590 IF H>.66 THEN H$="HIGH"
595 IF H<.33 THEN H$="LOW"
600 IF H>.32 AND H<.67 THEN H$="AVERAGE"
605 S=.99+(.01)*INT(RND(1)*9):REM**COST OF SUGAR
610 L=.69+(.01)*INT(RND(1)*9):REM**COST OF CONCENTRATE
615 C=.90+(.01)*INT(RND(1)*9):REM**COST OF CUPS
620 Z=-5+INT(RND(1)*20)
625 T2=T+Z-INT(H*10)
630 F2=INT(RND(1)*30)+70
635 F4=(((T+Z+T2)-137.5)/25)
640 IF F4<.1 THEN F4=.1
645 IF F4>1  THEN F4=1
650 GOSUB 1915 :REM CLEAR W
655 ? A0$(11) TAB(23) "PREDICTION FOR"
660 ? A0$(12) TAB(23) "SATURDAY:"
665 ? A0$(14) TAB(23) "TEMPERATURES IN"
670 ? A0$(15) TAB(23) "THE"T"\X9D'S AND"
675 ? A0$(16) TAB(23)  H$" HUMIDITY"
680 REM ------------------------------------
685 REM 
690 REM 
695 REM  BUY LEMON CONCENTRATE
700 REM 
705 REM 
710 REM ------------------------------------
715 GOSUB 1640 :REM SHOW VALUES
720 GOSUB 1970 :REM CLEAR D
725 ? A0$(23) "CONC. LEMON IS $"L"A QUART."
730 ? A0$(24) "HOW MANY QUARTS DO YOU WANT TO BUY? ";
735 GOSUB 2025 :D=AN 
740 IF D*L<B GOTO 765
745 ? "YOU DON'T HAVE ENOUGH MONEY"
750 ? "FOR "D" BOTTLES."
755 GOSUB 1745 :REM SPACEBAR
760 GOTO 715
765 B=B-(D*L) :D1=D1+D
770 GOSUB 1640 :REM SHOW VALUES
775 IF FX=2 GOTO 1025 :REM CHANGE
780 REM ------------------------------------
785 REM 
790 REM 
795 REM  BUY SUGAR
800 REM 
805 REM 
810 REM ------------------------------------
815 GOSUB 1640 :REM SHOW VALUES
820 GOSUB 1970 :REM CLEAR D
825 ? A0$(23) "SUGAR IS $"S"FOR A 5-LB BAG."
830 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
835 GOSUB 2025 :A=AN
840 IF A*S<B GOTO 865
845 ? A0$(25) "YOU DON'T HAVE ENOUGH MONEY"
850 ? A0$(26) "FOR" A "BAGS."
855 GOSUB 1745 :REM SPACEBAR
860 GOTO 815
865 B=B-(A*S) :A1=A1+(5*A)
870 GOSUB 1640 :REM SHOW VALUES
875 IF FX=2 GOTO 1025 :REM CHANGE
880 REM ------------------------------------
885 REM 
890 REM 
895 REM  BUY CUPS
900 REM 
905 REM 
910 REM ------------------------------------
915 GOSUB 1970 :REM CLEAR D
920 ? A0$(23) "CUPS ARE $"C"FOR A BAG OF 100."
925 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
930 GOSUB 2025 :P=AN
935 IF P*C<B GOTO 955
940 ? "YOU DON'T HAVE ENOUGH MONEY"
945 ? "FOR"P"BAGS."
950 GOTO 915
955 B=B-(P*C):P1=P1+(P*100)
960 GOSUB 1640 :REM SHOW VALUES
965 IF FX=2 GOTO 1025 :REM CHANGE
970 REM ------------------------------------
975 REM
980 REM
985 REM  CLEAR TEMPERATURE PREDICTION
990 REM  AND REPLACE WITH ACTUALS.
995 REM
1000 REM
1005 REM ------------------------------------
1010 GOSUB 1915 :REM CLEAR W
1015 ? A0$(12) TAB(23) "TEMPERATURE:"Z+T
1020 ? A0$(13) TAB(23) "WET BULB   :"T2
1025 GOSUB 1970 :REM CLEAR D
1030 ? A0$(23) "DO YOU WANT TO CHANGE ANY"
1035 ? "OF THE ITEMS? [YN]";
1040 GET YN$: IF YN$="N" THEN 1095
1045 IF YN$<>"Y" GOTO 1040
1050 FX=2
1055 GOSUB 1970 :REM CLEAR D
1060 ? A0$(23) "WHICH ITEM: LEMON=1   SUGAR=2   CUPS=3"
1065 NI$=""
1070 GET NI$
1075 IF NI$="1" THEN B=B+(A*S) :D1=D1-D       :GOTO 715
1080 IF NI$="2" THEN B=B+(D*L) :A1=A1-(5*A)   :GOTO 815
1085 IF NI$="3" THEN B=B+(P*C) :P1=P1-(P*100) :GOTO 915
1090 GOTO 1070
1095 GOSUB 1970 :REM CLEAR D
1100 ? A0$(23) "PRICE (IN CENTS) FOR A CUP"
1105 ? "OF LEMONADE: ";
1110 GOSUB 2025 :K=AN
1115 IF K<1 OR K>80 GOTO 1095
1120 F3=3-((K/3)/((((T+Z+T2)-37.5)^3)/10^6))
1125 IF F3<0 THEN F3=0
1130 GOSUB 1970 :REM CLEAR D
1135 ? A0$(9) TAB(9) K :REM PRICE PER CUP
1140 F1=INT(F2*F3*F4)
1145 IF F1<0 THEN F1=0
1150 IF F1<1 THEN W=1: IF F1<1 GOTO 1320
1155 REM ------------------------------------
1160 REM 
1165 REM 
1170 REM  RUN THE SIMULATION F1 TIMES
1175 REM
1180 REM 
1185 REM ------------------------------------
1190 FOR W=1 TO F1
1195    IF P1<.01 THENP1=0
1200    IF D1<.01 THEND1=0
1205    IF A1<.01 THENA1=0
1210    IF P1=0 OR A1=0 OR D1=0 GOTO 1245
1215    GOSUB 1640 :REM SHOW VALUES
1220    D1=D1-(1/40)
1225    A1=A1-(1/20)
1230    P1=P1-1
1235    B=B+(K*.01)
1240 NEXT W
1245 GOSUB 1640 :REM SHOW VALUES
1250 REM ------------------------------------
1255 REM 
1260 REM 
1265 REM  DISPLAY RESULTS AND COMMENTARY
1270 REM
1275 REM 
1280 REM ------------------------------------
1285 IF W=F1 GOTO 1320
1290 MM$="CUPS."
1295 IF P1<=0 THEN GOSUB 1850
1300 MM$="LEMON JUICE."
1305 IF D1<=0 THEN GOSUB 1850
1310 MM$="SUGAR."
1315 IF A1<=0 THEN GOSUB 1850
1320 GOSUB 1970 :REM CLEAR D
1325 ? A0$(23) "YOU MADE"W-1"SALES AT"K"CENTS A CUP."
1330 ? A0$(24) "YOU GROSSED $"(W-1)*(K/100)"\X9D."
1335 FOR K1=1 TO 50
1340    F5=3-((K1/3)/((((T+Z+T2)-37.5)^3/10^6)))
1345    G5=(INT(F5*F2*F4))*(K1/100)
1350    IF G5<G9 GOTO 1365
1355    G9=G5
1360    K9=K1
1365 NEXTK1
1370 IF K9=K GOTO 1390 :REM BEST PRICE
1375 ? A0$(25) "YOU COULD HAVE GROSSED $"G9"TODAY,"
1380 ? A0$(26) "IF YOU HAD CHARGED"K9"CENTS."
1385 GOTO 1405
1390 ? A0$(25)  K "CENTS WAS THE BEST PRICE FOR TODAY."
1395 IF FX<>1 THEN ? A0$(26) "YOU'RE A GOOD BUSINESSPERSON."
1400 COUNT=COUNT+1
1405 K9=0:G9=0
1410 GOSUB 1795 :REM WAIT
1415 GOSUB 1745 :REM SPACEBAR
1420 X=X+1: IF X<=WK GOTO 375 :REM LOOP
1425 REM ------------------------------------
1430 REM 
1435 REM 
1440 REM  END / SUMMARY
1445 REM
1450 REM 
1455 REM ------------------------------------
1460 ?CHR$(147)
1465 ? "\X12SUMMER'S END\X92"
1470 ?:? " THE SUMMER IS OVER."
1475 ?:? " YOU HAVE $"B"."
1480 ?:? " YOU OWE YOUR FAMILY $10, SO"
1485 ?:? " YOU EARNED $"B-10"THIS SUMMER."
1490 ?:? " YOU HAVE"P1"CUPS,"INT(D1)"BOTTLES OF"
1495 ?:? " LEMON JUICE, AND"INT(A1)"LBS OF"
1500 ?:? " SUGAR LEFT OVER."
1505 IF COUNT<WK/2 GOTO 1540
1510 ?
1515 ?:? " " N$ ", YOU ARE TRULY A GIFTED BUSINESS-"
1520 ?:? " PERSON. "; INT(COUNT/(X-1)*100);"% OF YOUR PRICES"
1525 ?:? " WERE THE BEST POSSIBLE!! "
1530 ?:? " THE ROCKEFELLERS AND GETTYS OF THE"
1535 ?:? " WORLD SALUTE YOU."
1540 GOSUB 1745
1545 SCREEN $02 :REM X16 BACK TO 80X60
1550 ?:?:? "THANK YOU FOR PLAYING!"
1555 END
1560 REM ------------------------------------
1565 REM
1570 REM
1575 REM
1580 REM       SUBROUTINES BEGIN HERE
1585 REM
1590 REM
1595 REM
1600 REM ------------------------------------
1605 REM ------------------------------------
1610 REM
1615 REM
1620 REM  SHOW VALUES (OLD LINE 3130)
1625 REM
1630 REM
1635 REM ------------------------------------
1640 FORU=1TO6
1645    IF MID$(STR$(B),U,1)="."GOTO 1660
1650 NEXT U
1655 GOTO 1670
1660 B=VAL(LEFT$(STR$(B),U+2))
1665 B=INT(B*100)/100
1670 U6=ABS(B)+1
1675 U7=INT(U6)
1680 U6$=STR$(U7-1)+"."+MID$(STR$(U6)+"00000",LEN(STR$(U7))+2,2)
1685 ? A0$(4) TAB(23)"          \X9D\X9D\X9D\X9D\X9D\X9D\X9D$" U6$
1690 ? A0$(5) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(D1)
1695 ? A0$(6) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(A1)
1700 ? A0$(7) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" P1
1705 RETURN
1710 REM ------------------------------------
1715 REM
1720 REM
1725 REM  PRESS SPACE TO CONTINUE
1730 REM 
1735 REM
1740 REM ------------------------------------
1745 ? A0$(27) "\X1D\X1D\X1D\X1D\X1D\X1D\X12 PRESS SPACEBAR TO CONTINUE "
1750 GETA$:IFA$<>" "THEN 1750
1755 RETURN
1760 REM ------------------------------------
1765 REM
1770 REM
1775 REM  A SHORT WAIT (100 JIFFYS)
1780 REM
1785 REM
1790 REM ------------------------------------
1795 TT=TI+100
1800 IF TT>TI GOTO 1800
1805 RETURN
1810 REM ------------------------------------
1815 REM
1820 REM
1825 REM  YOU WOULD HAVE MADE <F1> SALES, 
1830 REM           IF YOU HAD MORE <MM$>.
1835 REM
1840 REM
1845 REM ------------------------------------
1850 GOSUB 1970 :REM CLEAR D
1855 ? A0$(23) "YOU WOULD HAVE MADE"F1"SALES,"
1860 ? A0$(24) "IF YOU HAD MORE " MM$
1865 FX=1
1870 GOSUB 1795 :REM WAIT
1875 RETURN
1880 REM ------------------------------------
1885 REM
1890 REM
1895 REM  CLEAR WEATHERBOARD
1900 REM
1905 REM
1910 REM ------------------------------------
1915 FOR V=10 TO 18
1920 ? A0$(V) TAB(23) "                "
1925 NEXT V
1930 RETURN
1935 REM ------------------------------------
1940 REM
1945 REM
1950 REM  CLEAR DIRECTIVES
1955 REM
1960 REM
1965 REM ------------------------------------
1970 FOR V=23 TO 28
1975 ? A0$(V) "                                       "
1980 NEXT V
1985 RETURN
1990 REM ------------------------------------
1995 REM
2000 REM
2005 REM  DIGIT COLLECTION
2010 REM
2015 REM
2020 REM ------------------------------------
2025 GET A1$ :IF A1$<"0" OR A1$>"9" GOTO 2025
2030 ? A1$;
2035 GET A2$ :IF A2$="" GOTO 2035
2040 IF A2$=CHR$(20) THEN ? CHR$(20); :GOTO 2025
2045 IF A2$=CHR$(13) THEN A2$="" :GOTO 2075
2050 IF A2$<"0" OR A2$>"9" THEN 2035
2055 ? A2$;
2060 GET A3$ :IF A3$="" GOTO 2060
2065 IF A3$=CHR$(20) THEN ? CHR$(20); :GOTO 2035
2070 IF A3$<>CHR$(13) GOTO 2060
2075 AN=VAL(A1$+A2$)
2080 RETURN
2085 REM ------------------------------------
2090 REM
2095 REM
2100 REM  INTRODUCTION
2105 REM
2110 REM
2115 REM ------------------------------------
2120 ?CHR$(147);
2125 ? CHR$(14);
2130 ? " \X12LEMONADE STAND\X92"
2135 ?
2140 ? " YOU'VE DECIDED TO RUN A LEMONADE STAND"
2145 ? " FOR 12 SATURDAYS THIS SUMMER IN ORDER "
2150 ? " TO MAKE SOME MONEY. ÂUT YOU SOON FIND "
2155 ? " THAT RUNNING A STAND COSTS MONEY."
2160 ? " YOUR JOB IS TO MAKE AS BIG A PROFIT AS"
2165 ? " YOU CAN."
2170 ?
2175 ? " YOUR FAMILY AGREES TO STAKE YOU $10 IN"
2180 ? " ORDER TO GET STARTED. AT THE END OF "
2185 ? " THE SUMMER YOU WILL HAVE TO RETURN THE"
2190 ? " $10, BUT YOU WILL NOT NEED TO PAY"
2195 ? " INTEREST."
2200 ?
2205 ? " IF YOU KEEP YOUR PRICE LOW, YOU'LL SELL";
2210 ? " MORE LEMONADE, BUT WILL MAKE LESS MONEY";
2215 ? " ON EACH SALE."
2220 ? " ON HOT OR HUMID DAYS, YOU WILL SELL"
2225 ? " MORE LEMONADE AND ALSO BE ABLE TO"
2230 ? " CHARGE MORE PER CUP."
2235 ? " WHEN THE WET BULB READING IS CLOSE TO"
2240 ? " THE TEMPERATURE, IT IS A HUMID DAY."
2245 GOSUB 1795 :REM WAIT
2250 GOSUB 1745 :REM SPACEBAR
2255 ?CHR$(147):? CHR$(142)
2260 RETURN
2265 "-----------------------------------------------------
2270 :
2275 :
2280 "LEMONADE STAND DESIGN NOTES, R.EAGLESTONE 2021
2285 :
2290 :
2295 "-----------------------------------------------------
2300 "MY GOAL IS TO RE-CREATE THE ORIGINAL PET EXPERIENCE,
2305 "BUT ON THE X16 WITH A 40 X 25 DISPLAY.  I GO SO FAR AS 
2310 "TO USE THE ORIGINAL STARTING CASH AMOUNT, ORIGINAL 
2315 "IMPERIAL UNITS, AND A GREEN-ON-BLACK DISPLAY.
2320 :
2325 "THERE ARE A VERY FEW X16-SPECIFIC COMMANDS USED:
2330 "'COLOR' AND 'SCREEN' TO BE EXACT.
2335 :
2340 "WHAT IS UNCHANGED ARE THE ALGORITHMS USED TO 
2345 "CALCULATE THE PRICES, TEMPERATURES, AND DEMAND --
2350 "I.E. THE CORE MECHANICS OF THE GAME ITSELF.
2355 :
2360 "EFFORTS TO FIND THE SOURCE RETURNED TWO LATE VARIANTS:
2365 "(1) A VIC-20 LISTING MADE IN 2009
2370 "(2) THE C64 VERSION FROM 1986
2375 :
2380 "THE VIC-20 VERSION IS GREATLY STRIPPED DOWN, WHICH
2385 "ACTUALLY WAS A BOON SINCE IT LAID BARE THE FUNDAMENTAL
2390 "STRUCTURE OF THE GAME.
2395 :
2400 "THE C64 VERSION IS ABOUT 50% BOILERPLATE AND 
2405 "FRIVOLOUS POKES, INCLUDING SOME ML TO PREVENT THE
2410 "CODE FROM BEING MODIFIED.  AT THE SAME TIME, THAT
2415 "VERSION NOTES THAT THE PROGRAM WAS IN THE PUBLIC
2420 "DOMAIN. ITS CODE IS MORE FAITHFUL TO THE ORIGINAL,
2425 "AS WELL, ALTHOUGH DIFFERENCES EXIST.
2430 :
2435 "THIS VERSION WAS REBUILT USING THE VIC-20 VERSION
2440 "TO RECAPTURE THE ORIGINAL FLOW, THEN ADAPTING THE
2445 "1986 LOGIC INTO THE FRAMEWORK, PIECE BY PIECE.
2450 :
2455 "SUBROUTINES ARE NOT EQUIVALENT TO THE ORIGINALS.
2460 "IN FACT, EVEN THE INTERFACE IS NOT THE SAME, SINCE
2465 "I USED THE ORIGINAL PET VIEW AS THE TEMPLATE,
2470 "RATHER THAN THE C64/V20 VERSIONS, WHICH DIFFER.
